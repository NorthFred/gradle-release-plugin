apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'net.anshulverma.gradle.release'

apply from: "$rootDir/gradle/pom_config.gradle"
apply from: "$rootDir/gradle/package.gradle"

ext {
  gpgPassphrase = project.hasProperty('gpgPassphrase') ? project.property('gpgPassphrase') : System.env['GPG_PASSPHRASE']
  ossUser = project.hasProperty('ossUser') ? project.property('ossUser') : System.env['OSS_USER']
  ossPassword = project.hasProperty('ossPassword') ? project.property('ossPassword') : System.env['OSS_PASSWORD']

  releaseExt {
    versionedFiles << [
      'Readme.md': [
          47: '    classpath \'net.anshulverma.gradle:gradle-release-plugin:$currentVersion\''
      ]
    ]
  }
}

publishing {
  repositories {
    configure(rootProject.ext.releaseRepos)  // gradle-release-plugin sets up the release repos
  }

  publications {
    mavenJava(MavenPublication) {
      from components.java
      artifact packageSources {
        classifier "sources"
      }
      artifact(javadocJar) {
        classifier = 'javadoc'
      }
      pom.withXml pomConfig
    }
  }
}

bintray {
  user = bintrayUser
  key = bintrayKey
  publications = ['mavenJava']
  dryRun = project.hasProperty('dryRun') ? Boolean.valueOf(project.property('dryRun')) : false
  publish = true
  pkg {
    name = 'gradle-release-plugin'
    repo = 'gradle-plugins'
    desc = 'Gradle plugin that uses popular conventions to streamline releasing of a project.'
    websiteUrl = 'https://github.com/anshulverma/gradle-release-plugin'
    issueTrackerUrl = 'https://github.com/anshulverma/gradle-release-plugin/issues'
    vcsUrl = 'https://github.com/anshulverma/gradle-release-plugin.git'
    licenses = ['Apache-2.0']
    labels = ['gradle', 'plugin', 'release', 'versioning', 'publish', 'bintray']
    publicDownloadNumbers = true
    version {
      vcsTag = "v$project.version"
      attributes = ['gradle-plugin': 'net.anshulverma.gradle:gradle-release-plugin']

      gpg {
        sign = true
        passphrase = "$gpgPassphrase"
      }

      mavenCentralSync {
        sync = true
        user = "$ossUser"
        password = "$ossPassword"
        close = '1'
      }
    }
  }
}
